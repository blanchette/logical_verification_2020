import .love08_operational_semantics_demo


/-! # LoVe Demo 10: Denotational Semantics

We review a third way to specify the semantics of a programming language:
denotational semantics. Denotational semantics attempt to directly specify the
meaning of programs.

If operational semantics is an idealized interpreter and Hoare logic is an
idealized verifier, then denotational semantics is an idealized compiler.-/


set_option pp.beta true

namespace LoVe


/-! ## Compositionality

A __denotational semantics__ defines the meaning of each program as a
mathematical object:

    `âŸ¦ âŸ§ : syntax â†’ semantics`

A key property of denotational semantics is __compositionality__: The meaning of
a compound statement should be defined in terms of the meaning of its
components. This disqualifies

    `âŸ¦SâŸ§ = {st | (S, prod.fst st) âŸ¹ prod.snd st}`

i.e.

    `âŸ¦SâŸ§ = {(s, t) | (S, s) âŸ¹ t}`

because operational semantics are not compositional.

In short, we want

    `âŸ¦S ; TâŸ§              = â€¦ âŸ¦SâŸ§ â€¦ âŸ¦TâŸ§ â€¦`
    `âŸ¦if b then S else TâŸ§ = â€¦ âŸ¦SâŸ§ â€¦ âŸ¦TâŸ§ â€¦`
    `âŸ¦while b do SâŸ§       = â€¦ âŸ¦SâŸ§ â€¦`

An evaluation function on arithmetic expressions

    `eval : aexp â†’ ((string â†’ â„¤) â†’ â„¤)`

is a denotational semantics. We want the same for imperative programs.


## A Relational Denotational Semantics

We can represent the semantics of an imperative program as a function from
initial state to final state or more generally as a relation between initial
state and final state: `set (state Ã— state)`.

For `skip`, `:=`, `;`, and `if then else`, the denotational semantics is
easy: -/

namespace sorry_defs

def denote : stmt â†’ set (state Ã— state)
| stmt.skip         := Id
| (stmt.assign n a) :=
  {st | prod.snd st = (prod.fst st){n â†¦ a (prod.fst st)}}
| (stmt.seq S T)    := denote S â—¯ denote T
| (stmt.ite b S T)  := (denote S â‡ƒ b) âˆª (denote T â‡ƒ (Î»s, Â¬ b s))
| (stmt.while b S)  := sorry

end sorry_defs

/-! We write `âŸ¦SâŸ§` for `denote S`. For `while`, we would like to write

    `((denote S â—¯ denote (stmt.while b S)) â‡ƒ b) âˆª (Id â‡ƒ (Î»s, Â¬ b s))`

but this is ill-founded due to the recursive call on `stmt.while b S`.

What we are looking for is an `X` such that

    `X = ((denote S â—¯ X) â‡ƒ b) âˆª (Id â‡ƒ (Î»s, Â¬ b s))`

In other words, we are looking for a fixpoint.

Most of this lecture is concerned with building a least fixpoint operator
`lfp` that will allow us to define the `while` case as well:

    `lfp (Î»X, ((denote S â—¯ X) â‡ƒ b) âˆª (Id â‡ƒ (Î»s, Â¬ b s)))`


## Fixpoints

A __fixpoint__ (or fixed point) of `f` is a solution for `X` in the equation

    `X = f X`

In general, fixpoints may not exist at all (e.g., `f := nat.succ`), or there may
be several fixpoints (e.g., `f := id`). But under some conditions on `f`, a
unique __least fixpoint__ and a unique __greatest fixpoint__ are guaranteed to
exist.

Consider this __fixpoint equation__:

    `X = (Î»(p : â„• â†’ Prop) (n : â„•), n = 0 âˆ¨ âˆƒm : â„•, n = m + 2 âˆ§ p m) X`
      `= (Î»n : â„•, n = 0 âˆ¨ âˆƒm : â„•, n = m + 2 âˆ§ X m)`

where `X : â„• â†’ Prop` and
`f := (Î»(p : â„• â†’ Prop) (n : â„•), n = 0 âˆ¨ âˆƒm : â„•, n = m + 2 âˆ§ p m)`.

The above example admits only one fixpoint. The fixpoint equation uniquely
specifies `X` as the set of even numbers.

In general, the least and greatest fixpoint may be different:

    `X = X`

Here, the least fixpoint is `(Î»_, False)` and the greatest fixpoint is
`(Î»_, True)`. Conventionally, `False < True`, and thus
`(Î»_, False) < (Î»_, True)`. Similarly, `âˆ… < @set.univ Î±` (assuming `Î±` is
inhabited).

For the semantics of programming languages:

* `X` will have type `set (state Ã— state)` (which is isomorphic to
  `state â†’ state â†’ Prop`), representing relations between states;

* `f` will correspond to either taking one extra iteration of the loop (if the
  condition `b` is true) or the identity (if `b` is false).

Kleene's fixpoint theorem:

    `f^0(âˆ…) âˆª f^1(âˆ…) âˆª f^2(âˆ…) âˆª â‹¯ = lfp f`

The least fixpoint corresponds to finite executions of a program, which is all
we care about.

**Key observation**:

    Inductive predicates correspond to least fixpoints, but they are built into
    Lean's logic (the calculus of inductive constructions).


## Monotone Functions

Let `Î±` and `Î²` be types with partial order `â‰¤`. A function `f : Î± â†’ Î²` is
__monotone__ if

    `a â‰¤ b â†’ f a â‰¤ f b`   for all `a`, `b`

Many operations on sets (e.g., `âˆª`), relations (e.g., `â—¯`), and functions
(e.g., `Î»x, x`, `Î»_, k`, `âˆ˜`) are monotone or preserve monotonicity.

All monotone functions `f : set Î± â†’ set Î±` admit least and greatest fixpoints.

**Example of a nonmonotone function**:

    `f A = (if A = âˆ… then set.univ else âˆ…)`

Assuming `Î±` is inhabited, we have `âˆ… âŠ† set.univ`, but
`f âˆ… = set.univ âŠˆ âˆ… = f set.univ`. -/

def monotone {Î± Î² : Type} [partial_order Î±] [partial_order Î²]
  (f : Î± â†’ Î²) : Prop :=
âˆ€aâ‚ aâ‚‚, aâ‚ â‰¤ aâ‚‚ â†’ f aâ‚ â‰¤ f aâ‚‚

lemma monotone_id {Î± : Type} [partial_order Î±] :
  monotone (Î»a : Î±, a) :=
begin
  intros aâ‚ aâ‚‚ ha,
  exact ha
end

lemma monotone_const {Î± Î² : Type} [partial_order Î±]
    [partial_order Î²] (b : Î²) :
  monotone (Î»_ : Î±, b) :=
begin
  intros aâ‚ aâ‚‚ ha,
  exact le_refl b
end

lemma monotone_union {Î± Î² : Type} [partial_order Î±]
    (f g : Î± â†’ set Î²) (hf : monotone f) (hg : monotone g) :
  monotone (Î»a, f a âˆª g a) :=
begin
  intros aâ‚ aâ‚‚ ha b hb,
  cases hb,
  { exact or.intro_left _ (hf aâ‚ aâ‚‚ ha hb) },
  { exact or.intro_right _ (hg aâ‚ aâ‚‚ ha hb) }
end

/-! We will prove the following two lemmas in the exercise. -/

namespace sorry_lemmas

lemma monotone_comp {Î± Î² : Type} [partial_order Î±]
    (f g : Î± â†’ set (Î² Ã— Î²)) (hf : monotone f)
    (hg : monotone g) :
  monotone (Î»a, f a â—¯ g a) :=
sorry

lemma monotone_restrict {Î± Î² : Type} [partial_order Î±]
    (f : Î± â†’ set (Î² Ã— Î²)) (p : Î² â†’ Prop) (hf : monotone f) :
  monotone (Î»a, f a â‡ƒ p) :=
sorry

end sorry_lemmas


/-! ## Complete Lattices

To define the least fixpoint on sets, we need `âŠ†` and `â‹‚`. Complete lattices
capture this concept abstractly. A __complete lattice__ is an ordered type `Î±`
for which each set of type `set Î±` has an infimum.

More precisely, A complete lattice consists of

* a partial order `â‰¤ : Î± â†’ Î± â†’ Prop` (i.e., a reflexive, transitive, and
  antisymmetric binary predicate);

* an operator `Inf : set Î± â†’ Î±`, called __infimum__.

Moreover, `Inf A` must satisfy these two properties:

* `Inf A` is a lower bound of `A`: `Inf A â‰¤ b` for all `b âˆˆ A`;

* `Inf A` is a greatest lower bound: `b â‰¤ Inf A` for all `b` such that
  `âˆ€a, a âˆˆ A â†’ b â‰¤ a`.

**Warning:** `Inf A` is not necessarily an element of `A`.

Examples:

* `set Î±` is an instance w.r.t. `âŠ†` and `â‹‚` for all `Î±`;
* `Prop` is an instance w.r.t. `â†’` and `âˆ€` (`Inf A := âˆ€a âˆˆ A, a`);
* `enat := â„• âˆª {âˆž}`;
* `ereal := â„ âˆª {- âˆž, âˆž}`;
* `Î² â†’ Î±` if `Î±` is a complete lattice;
* `Î± Ã— Î²` if `Î±`, `Î²` are complete lattices.

Finite example (with apologies for the ASCII art):

                Z            Inf {}           = ?
              /   \          Inf {Z}          = ?
             A     B         Inf {A, B}       = ?
              \   /          Inf {Z, A}       = ?
                Y            Inf {Z, A, B, Y} = ?

Nonexamples:

* `â„•`, `â„¤`, `â„š`, `â„`: no infimum for `âˆ…`, `Inf â„•`, etc.
* `erat := â„š âˆª {- âˆž, âˆž}`: `Inf {q | 2 < q * q} = sqrt 2` is not in `erat`. -/

@[class] structure complete_lattice (Î± : Type)
  extends partial_order Î± : Type :=
(Inf    : set Î± â†’ Î±)
(Inf_le : âˆ€A b, b âˆˆ A â†’ Inf A â‰¤ b)
(le_Inf : âˆ€A b, (âˆ€a, a âˆˆ A â†’ b â‰¤ a) â†’ b â‰¤ Inf A)

/-! For sets: -/

@[instance] def set.complete_lattice {Î± : Type} :
  complete_lattice (set Î±) :=
{ le          := (âŠ†),
  le_refl     := by tautology,
  le_trans    := by tautology,
  le_antisymm :=
    begin
      intros A B hAB hBA,
      apply set.ext,
      tautology
    end,
  Inf         := Î»ð’œ, {a | âˆ€A, A âˆˆ ð’œ â†’ a âˆˆ A},
  Inf_le      := by tautology,
  le_Inf      := by tautology }


/-! ## Least Fixpoint -/

def lfp {Î± : Type} [complete_lattice Î±] (f : Î± â†’ Î±) : Î± :=
complete_lattice.Inf ({a | f a â‰¤ a})

lemma lfp_le {Î± : Type} [complete_lattice Î±] (f : Î± â†’ Î±)
    (a : Î±) (h : f a â‰¤ a) :
  lfp f â‰¤ a :=
complete_lattice.Inf_le _ _ h

lemma le_lfp {Î± : Type} [complete_lattice Î±] (f : Î± â†’ Î±)
    (a : Î±) (h : âˆ€a', f a' â‰¤ a' â†’ a â‰¤ a') :
  a â‰¤ lfp f :=
complete_lattice.le_Inf _ _ h

/-! **Knaster-Tarski theorem:** For any monotone function `f`:

* `lfp f` is a fixpoint: `lfp f = f (lfp f)` (lemma `lfp_eq`);
* `lfp f` is smaller than any other fixpoint: `X = f X â†’ lfp f â‰¤ X`. -/

lemma lfp_eq {Î± : Type} [complete_lattice Î±] (f : Î± â†’ Î±)
    (hf : monotone f) :
  lfp f = f (lfp f) :=
begin
  have h : f (lfp f) â‰¤ lfp f :=
    begin
      apply le_lfp,
      intros a' ha',
      apply @le_trans _ _ _ (f a'),
      { apply hf,
        apply lfp_le,
        assumption },
      { assumption }
    end,
  apply le_antisymm,
  { apply lfp_le,
    apply hf,
    assumption },
  { assumption }
end


/-! ## A Relational Denotational Semantics, Continued -/

def denote : stmt â†’ set (state Ã— state)
| stmt.skip         := Id
| (stmt.assign x a) :=
  {st | prod.snd st = (prod.fst st){x â†¦ a (prod.fst st)}}
| (stmt.seq S T)    := denote S â—¯ denote T
| (stmt.ite b S T)  := (denote S â‡ƒ b) âˆª (denote T â‡ƒ (Î»s, Â¬ b s))
| (stmt.while b S)  :=
  lfp (Î»X, ((denote S â—¯ X) â‡ƒ b) âˆª (Id â‡ƒ (Î»s, Â¬ b s)))

notation `âŸ¦` S `âŸ§`:= denote S


/-! ## Application to Program Equivalence

Based on the denotational semantics, we introduce the notion of program
equivalence: `Sâ‚ ~ Sâ‚‚`. (Compare with exercise 8.) -/

def denote_equiv (Sâ‚ Sâ‚‚ : stmt) : Prop :=
âŸ¦Sâ‚âŸ§ = âŸ¦Sâ‚‚âŸ§

infix ` ~ ` := denote_equiv

/-! It is obvious from the definition that `~` is an equivalence relation.

Program equivalence can be used to replace subprograms by other subprograms with
the same semantics. This is achieved by the following congruence rules: -/

lemma denote_equiv.seq_congr {Sâ‚ Sâ‚‚ Tâ‚ Tâ‚‚ : stmt}
    (hS : Sâ‚ ~ Sâ‚‚) (hT : Tâ‚ ~ Tâ‚‚) :
  Sâ‚ ;; Tâ‚ ~ Sâ‚‚ ;; Tâ‚‚ :=
by simp [denote_equiv, denote, *] at *

lemma denote_equiv.ite_congr {b} {Sâ‚ Sâ‚‚ Tâ‚ Tâ‚‚ : stmt}
    (hS : Sâ‚ ~ Sâ‚‚) (hT : Tâ‚ ~ Tâ‚‚) :
  stmt.ite b Sâ‚ Tâ‚ ~ stmt.ite b Sâ‚‚ Tâ‚‚ :=
by simp [denote_equiv, denote, *] at *

lemma denote_equiv.while_congr {b} {Sâ‚ Sâ‚‚ : stmt}
    (hS : Sâ‚ ~ Sâ‚‚) :
  stmt.while b Sâ‚ ~ stmt.while b Sâ‚‚ :=
by simp [denote_equiv, denote, *] at *

/-! Compare the simplicity of these proofs with the corresponding proofs for a
big-step semantics (exercise 8).

Let us prove some program equivalences. -/

lemma denote_equiv.skip_assign_id {x} :
  stmt.assign x (Î»s, s x) ~ stmt.skip :=
by simp [denote_equiv, denote, Id]

lemma denote_equiv.seq_skip_left {S : stmt} :
  stmt.skip ;; S ~ S :=
by simp [denote_equiv, denote, Id, comp]

lemma denote_equiv.seq_skip_right {S : stmt} :
  S ;; stmt.skip ~ S :=
by simp [denote_equiv, denote, Id, comp]

lemma denote_equiv.ite_seq_while {b} {S : stmt} :
  stmt.ite b (S ;; stmt.while b S) stmt.skip ~ stmt.while b S :=
begin
  simp [denote_equiv, denote],
  apply eq.symm,
  apply lfp_eq,
  apply monotone_union,
  { apply sorry_lemmas.monotone_restrict,
    apply sorry_lemmas.monotone_comp,
    { exact monotone_const _ },
    { exact monotone_id } },
  { apply sorry_lemmas.monotone_restrict,
    exact monotone_const _ }
end


/-! ## Equivalence of the Denotational and the Big-Step Semantics
## (**optional**) -/

lemma denote_of_big_step (S : stmt) (s t : state) :
  (S, s) âŸ¹ t â†’ (s, t) âˆˆ âŸ¦SâŸ§ :=
begin
  generalize hSs : (S, s) = Ss,
  intro h,
  induction h generalizing S s; cases hSs;
    try { solve1 { simp [denote, *] } },
  { apply exists.intro h_t,
    tautology },
  { rewrite eq.symm denote_equiv.ite_seq_while,
    simp [denote, *],
    apply exists.intro h_t,
    apply and.intro,
    { tautology },
    { apply h_ih_hrest (stmt.while h_b h_S),
      refl } },
  { rewrite eq.symm denote_equiv.ite_seq_while,
    simp [denote, *] }
end

lemma big_step_of_denote :
  âˆ€(S : stmt) (s t : state), (s, t) âˆˆ âŸ¦SâŸ§ â†’ (S, s) âŸ¹ t
| stmt.skip         s t := by simp [denote]
| (stmt.assign n a) s t := by simp [denote]
| (stmt.seq S T)    s t :=
  begin
    intro h,
    cases h with u hu,
    exact big_step.seq
      (big_step_of_denote S _ _ (and.elim_left hu))
      (big_step_of_denote T _ _ (and.elim_right hu))
  end
| (stmt.ite b S T)  s t :=
  begin
    intro h,
    cases h,
    case or.inl {
      cases h,
      apply big_step.ite_true h_left,
      exact big_step_of_denote _ _ _ h_right },
    case or.inr {
      cases h,
      apply big_step.ite_false h_left,
      exact big_step_of_denote _ _ _ h_right }
  end
| (stmt.while b S)  s t :=
  begin
    have hw : âŸ¦stmt.while b SâŸ§
        â‰¤ {st | (stmt.while b S, prod.fst st) âŸ¹ prod.snd st} :=
      begin
        apply lfp_le _ _ _,
        intros x hx,
        cases x with s t,
        simp at hx,
        cases hx,
        case or.inl {
          cases hx with hs hst,
          cases hst with u hu,
          apply big_step.while_true hs,
          { exact big_step_of_denote S _ _ (and.elim_left hu) },
          { exact and.elim_right hu } },
        case or.inr {
          cases hx,
          cases hx_right,
          apply big_step.while_false hx_left }
      end,
    apply hw
  end

lemma denote_iff_big_step (S : stmt) (s t : state) :
  (s, t) âˆˆ âŸ¦SâŸ§ â†” (S, s) âŸ¹ t :=
iff.intro (big_step_of_denote S s t) (denote_of_big_step S s t)

end LoVe
